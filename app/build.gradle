apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: '../version.gradle'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "org.blokada"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode extVersionCode
        versionName extVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        dev.java.srcDirs += 'src/dev/kotlin'
        prod.java.srcDirs += 'src/prod/kotlin'
        firebase.java.srcDirs += 'src/firebase/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
    }
    signingConfigs {
        release {
            keyAlias 'blokada'
            keyPassword BLOKADA_KEY_PASSWORD
            storeFile file(BLOKADA_KEY_PATH)
            storePassword BLOKADA_STORE_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        firebase {
            initWith(release)
            versionNameSuffix '-fb'
        }
        debug {
            versionNameSuffix '-debug'
        }
    }
    flavorDimensions("blokada")
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
        }
        prod {
            applicationIdSuffix ".alarm"
        }
    }
}

dependencies {
    // Core library for kar.gs projects
    implementation project(':gscore')

    // Android support libraries
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:design:27.0.2'

    // Kotlin and its dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'nl.komponents.kovenant:kovenant-core:3.3.0'
    implementation 'nl.komponents.kovenant:kovenant-android:3.3.0'

    // Proxy
    implementation 'org.pcap4j:pcap4j-core:1.7.0'
    implementation 'org.pcap4j:pcap4j-packetfactory-propertiesbased:1.7.0'
    implementation 'dnsjava:dnsjava:2.1.8'

    // UI libraries
    implementation ('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true
    }

    // Analytics and debugging
    firebaseImplementation 'com.google.firebase:firebase-core:11.8.0'
    firebaseImplementation 'com.google.firebase:firebase-crash:11.8.0'

    // Test dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

buildscript {
    ext.kotlin_version = '1.2.21'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
