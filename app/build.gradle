buildscript {
    ext.kotlin_version = '1.2.41'
    ext.acraVersion = '5.1.3'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.gladed.androidgitversion' version '0.4.3'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

androidGitVersion {
    codeFormat 'MNNPPPPPP'
    format '%tag%'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 27
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
    defaultConfig {
        applicationId "org.blokada"
        minSdkVersion 21
        targetSdkVersion 27
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        // Modules do NOT make a runnable app separately
        moduleCore.java.srcDirs += 'src/core/kotlin'
        testModuleCore.java.srcDirs += 'src/test-core/kotlin'

        moduleTunnel.java.srcDirs += 'src/core/kotlin'
        moduleTunnel.java.srcDirs += 'src/tunnel/kotlin'
        testModuleTunnel.java.srcDirs += 'src/test-core/kotlin'
        testModuleTunnel.java.srcDirs += 'src/test-tunnel/kotlin'

        moduleG11n.java.srcDirs += 'src/core/kotlin'
        moduleG11n.java.srcDirs += 'src/g11n/kotlin'
        testModuleG11n.java.srcDirs += 'src/test-core/kotlin'
        testModuleG11n.java.srcDirs += 'src/test-g11n/kotlin'

        moduleLegacy.java.srcDirs += 'src/core/kotlin'
        moduleLegacy.java.srcDirs += 'src/tunnel/kotlin'
        moduleLegacy.java.srcDirs += 'src/legacy/kotlin'
        moduleLegacy.res.srcDirs += 'src/legacy/res'
        testModuleLegacy.java.srcDirs += 'src/test-core/kotlin'
        testModuleLegacy.java.srcDirs += 'src/test-tunnel/kotlin'
        testModuleLegacy.java.srcDirs += 'src/test-legacy/kotlin'

        // Flavors put together modules and flavor config
        adblockerHome.manifest.srcFile 'src/legacy/AndroidManifest.xml'
        adblockerHome.java.srcDirs += 'src/core/kotlin'
        adblockerHome.java.srcDirs += 'src/tunnel/kotlin'
        adblockerHome.java.srcDirs += 'src/g11n/kotlin'
        adblockerHome.java.srcDirs += 'src/legacy/kotlin'
        adblockerHome.java.srcDirs += 'src/flavor-adblocker/kotlin'
        adblockerHome.res.srcDirs += 'src/legacy/res'
        adblockerHome.res.srcDirs += 'src/flavor-adblocker/res'
        testAdblockerHome.java.srcDirs += 'src/test-core/kotlin'
        testAdblockerHome.java.srcDirs += 'src/test-tunnel/kotlin'
        testAdblockerHome.java.srcDirs += 'src/test-g11n/kotlin'

        adblockerFdroid.manifest.srcFile 'src/legacy/AndroidManifest.xml'
        adblockerFdroid.java.srcDirs += 'src/core/kotlin'
        adblockerFdroid.java.srcDirs += 'src/tunnel/kotlin'
        adblockerFdroid.java.srcDirs += 'src/g11n/kotlin'
        adblockerFdroid.java.srcDirs += 'src/legacy/kotlin'
        adblockerFdroid.java.srcDirs += 'src/flavor-adblocker/kotlin'
        adblockerFdroid.res.srcDirs += 'src/legacy/res'
        adblockerFdroid.res.srcDirs += 'src/flavor-adblocker/res'

        dnsHome.manifest.srcFile 'src/legacy/AndroidManifest.xml'
        dnsHome.java.srcDirs += 'src/core/kotlin'
        dnsHome.java.srcDirs += 'src/tunnel/kotlin'
        dnsHome.java.srcDirs += 'src/g11n/kotlin'
        dnsHome.java.srcDirs += 'src/legacy/kotlin'
        dnsHome.java.srcDirs += 'src/flavor-dns/kotlin'
        dnsHome.res.srcDirs += 'src/legacy/res'
        dnsHome.res.srcDirs += 'src/flavor-dns/res'

        // Build types are cross combined with flavors to make a final release
        debug.java.srcDirs += 'src/build-unofficial/kotlin'
        release.java.srcDirs += 'src/build-official/kotlin'
        release.res.srcDirs += 'src/build-official/res'
        release.java.srcDirs += 'src/build-official-imports/kotlin'
        official.java.srcDirs += 'src/build-official/kotlin'
        official.res.srcDirs += 'src/build-official/res'
        official.java.srcDirs += 'src/build-official-imports/kotlin'
        beta.java.srcDirs += 'src/build-official/kotlin'
        beta.res.srcDirs += 'src/build-official/res'
        beta.java.srcDirs += 'src/build-official-imports/kotlin'
    }
    signingConfigs {
        release {
            keyAlias 'blokada'
            keyPassword 'default'
            storeFile file('default')
            storePassword 'default'
        }
    }
    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        official {
            initWith(release)
            matchingFallbacks = ['release']
        }
        beta {
            initWith(release)
            matchingFallbacks = ['release']
        }
    }
    flavorDimensions("model")
    productFlavors {
        adblockerHome {
            applicationId 'org.blokada.origin.alarm'
        }
        adblockerFdroid {
            applicationId "org.blokada.alarm"
        }
        dnsHome {
            applicationId "org.blokada.alarm.dnschanger"
        }
        moduleCore {}
        moduleTunnel {}
        moduleG11n {}
        moduleLegacy {}
    }
}

dependencies {
    // Android support libraries
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    // Kotlin and its dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'nl.komponents.kovenant:kovenant-core:3.3.0'
    implementation 'nl.komponents.kovenant:kovenant-android:3.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.23.1'
    implementation 'io.paperdb:paperdb:2.6'
    implementation 'com.github.michaelbull:kotlin-result:1.0.8'
    // Proxy
    implementation 'org.pcap4j:pcap4j-core:1.7.0'
    implementation 'org.pcap4j:pcap4j-packetfactory-propertiesbased:1.7.0'
    implementation 'dnsjava:dnsjava:2.1.8'
    // UI libraries
    implementation('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true
    }
    // Testing and debugging
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-limiter:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
