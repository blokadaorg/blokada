buildscript {
    ext.kotlin_version = '1.3.30'
    ext.acraVersion = '5.1.3'
    ext.retrofitVersion = '2.5.0'
    ext.okhttpVersion = '3.14.0'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.gladed.androidgitversion' version '0.4.7'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

androidGitVersion {
    codeFormat 'MNNPPPPPP'
    format '%tag%'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
    defaultConfig {
        applicationId "org.blokada"
        minSdkVersion 21
        targetSdkVersion 28
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86'
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        // Modules do NOT make a runnable app separately
        // But you can actually choose a module-only build variant to work on it
        moduleCore.java.srcDirs += 'src/core/kotlin'
        testModuleCore.java.srcDirs += 'src/test-core/kotlin'

        moduleTunnel.java.srcDirs += 'src/core/kotlin'
        moduleTunnel.java.srcDirs += 'src/tunnel/kotlin'
        testModuleTunnel.java.srcDirs += 'src/test-core/kotlin'
        testModuleTunnel.java.srcDirs += 'src/test-tunnel/kotlin'

        moduleG11n.java.srcDirs += 'src/core/kotlin'
        moduleG11n.java.srcDirs += 'src/g11n/kotlin'
        testModuleG11n.java.srcDirs += 'src/test-core/kotlin'
        testModuleG11n.java.srcDirs += 'src/test-g11n/kotlin'

        moduleVpn.java.srcDirs += 'src/core/kotlin'
        moduleVpn.java.srcDirs += 'src/vpn/kotlin'
        moduleVpn.jniLibs.srcDirs += 'src/vpn/jniLibs'
        testModuleVpn.java.srcDirs += 'src/test-core/kotlin'
        testModuleVpn.java.srcDirs += 'src/test-vpn/kotlin'

        moduleLegacy.java.srcDirs += 'src/core/kotlin'
        moduleLegacy.java.srcDirs += 'src/tunnel/kotlin'
        moduleLegacy.java.srcDirs += 'src/legacy/kotlin'
        moduleLegacy.res.srcDirs += 'src/legacy/res'
        testModuleLegacy.java.srcDirs += 'src/test-core/kotlin'
        testModuleLegacy.java.srcDirs += 'src/test-tunnel/kotlin'
        testModuleLegacy.java.srcDirs += 'src/test-legacy/kotlin'

        // Flavors put together modules and flavor config
        adblockerHome.manifest.srcFile 'src/legacy/AndroidManifest.xml'
        adblockerHome.java.srcDirs += 'src/core/kotlin'
        adblockerHome.java.srcDirs += 'src/tunnel/kotlin'
        adblockerHome.java.srcDirs += 'src/g11n/kotlin'
        adblockerHome.java.srcDirs += 'src/vpn/kotlin'
        adblockerHome.java.srcDirs += 'src/legacy/kotlin'
        adblockerHome.java.srcDirs += 'src/flavor-adblocker/kotlin'
        adblockerHome.jniLibs.srcDirs += 'src/vpn/jniLibs'
        adblockerHome.res.srcDirs += 'src/legacy/res'
        adblockerHome.res.srcDirs += 'src/flavor-adblocker/res'
        testAdblockerHome.java.srcDirs += 'src/test-core/kotlin'
        testAdblockerHome.java.srcDirs += 'src/test-tunnel/kotlin'
        testAdblockerHome.java.srcDirs += 'src/test-g11n/kotlin'
        testAdblockerHome.java.srcDirs += 'src/test-vpn/kotlin'

        adblockerFdroid.manifest.srcFile 'src/legacy/AndroidManifest.xml'
        adblockerFdroid.java.srcDirs += 'src/core/kotlin'
        adblockerFdroid.java.srcDirs += 'src/tunnel/kotlin'
        adblockerFdroid.java.srcDirs += 'src/g11n/kotlin'
        adblockerFdroid.java.srcDirs += 'src/vpn/kotlin'
        adblockerFdroid.java.srcDirs += 'src/legacy/kotlin'
        adblockerFdroid.java.srcDirs += 'src/flavor-adblocker/kotlin'
        adblockerFdroid.jniLibs.srcDirs += 'src/vpn/jniLibs'
        adblockerFdroid.res.srcDirs += 'src/legacy/res'
        adblockerFdroid.res.srcDirs += 'src/flavor-adblocker/res'

        dnsHome.manifest.srcFile 'src/legacy/AndroidManifest.xml'
        dnsHome.java.srcDirs += 'src/core/kotlin'
        dnsHome.java.srcDirs += 'src/tunnel/kotlin'
        dnsHome.java.srcDirs += 'src/g11n/kotlin'
        dnsHome.java.srcDirs += 'src/legacy/kotlin'
        dnsHome.java.srcDirs += 'src/flavor-dns/kotlin'
        dnsHome.res.srcDirs += 'src/legacy/res'
        dnsHome.res.srcDirs += 'src/flavor-dns/res'

        // Build types are cross combined with flavors to make a final release
        debug.java.srcDirs += 'src/build-unofficial/kotlin'
        release.java.srcDirs += 'src/build-official/kotlin'
        release.res.srcDirs += 'src/build-official/res'
        official.java.srcDirs += 'src/build-official/kotlin'
        official.res.srcDirs += 'src/build-official/res'
        beta.java.srcDirs += 'src/build-official/kotlin'
        beta.res.srcDirs += 'src/build-official/res'
    }
    signingConfigs {
        release {
            keyAlias 'blokada'
            keyPassword BLOKADA_KEY_PASSWORD
            storeFile file(BLOKADA_KEY_PATH)
            storePassword BLOKADA_STORE_PASSWORD
        }
    }
    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        official {
            initWith(release)
            matchingFallbacks = ['release']
        }
        beta {
            initWith(release)
            matchingFallbacks = ['release']
        }
    }
    flavorDimensions("model")
    productFlavors {
        adblockerHome {
            applicationId "org.blokada.origin.alarm"
        }
        adblockerFdroid {
            applicationId "org.blokada.alarm"
        }
        dnsHome {
            applicationId "org.blokada.alarm.dnschanger"
        }
        moduleCore {}
        moduleTunnel {}
        moduleG11n {}
        moduleVpn {}
        moduleLegacy {}
    }
}

dependencies {
    // Android support libraries
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    // Kotlin and its dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'nl.komponents.kovenant:kovenant-core:3.3.0'
    implementation 'nl.komponents.kovenant:kovenant-android:3.3.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.23.1"
//    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-M1'
    implementation 'io.paperdb:paperdb:2.6'
    implementation 'com.github.michaelbull:kotlin-result:1.0.8'

    // Proxy
    implementation 'org.pcap4j:pcap4j-core:1.7.0'
    implementation 'org.pcap4j:pcap4j-packetfactory-propertiesbased:1.7.0'
    implementation 'dnsjava:dnsjava:2.1.8'
    implementation 'org.slf4j:slf4j-nop:1.6.1'

    // UI libraries
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'com.ramotion.foldingcell:folding-cell:1.2.2'
    implementation 'jp.wasabeef:blurry:3.0.0'
    implementation 'com.github.ps100000:PageIndicatorView:v1.0.4'

    // REST api
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation ("com.squareup.retrofit2:retrofit:$retrofitVersion"){
        exclude module: 'okhttp'
    }
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Testing and debugging
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
